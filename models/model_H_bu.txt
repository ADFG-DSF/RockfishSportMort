
model{
 tau_comp <- 1 / sd_comp / sd_comp
 sd_comp ~ dunif(0, 10)
 lambda_bc ~ dgamma(0.001, 0.001)
 
 for(n in 1:N){ #composition likelihoods; comp_ denoted by x_ayu in rmarkdown...
  comp_pelagic[n] ~ dbin(p_pelagic[comp_area[n], comp_year[n], comp_user[n] + 1], comp_N[n])
  comp_black[n] ~ dbin(p_black[comp_area[n], comp_year[n] , comp_user[n] + 1], comp_pelagic[n])
  comp_yellow[n] ~ dbin(p_yellow[comp_area[n], comp_year[n], comp_user[n] + 1], comp_N[n] - comp_pelagic[n])
    }
 
 for(r in 1:3){ #priors for mean composition
  mu_beta0_pelagic[r] ~ dnorm(0, 0.001)
  tau_beta0_pelagic[r] ~ dgamma(0.001, 0.001)
  mu_beta0_yellow[r] ~ dnorm(0, 0.001)
  tau_beta0_yellow[r] ~ dgamma(0.001, 0.001)
  mu_beta0_black[r] ~ dnorm(0, 0.001)
  tau_beta0_black[r] ~ dgamma(0.001, 0.001)
  }

 for (a in 1:A){
  mu_bc[a] ~ dnorm(0, 0.001)              # mean log bias in SWHS H estimates
  tau_bc[a] <- 1 / sd_bc[a] / sd_bc[a]    # transformed sigma in mean log bias 
  sd_bc[a] ~ dunif(0, 10)                 # sigma mean log bias?? 
  b1_pG[a] ~ dunif(1, 50)                 # beta1 for user proportion (Puser)
  b2_pG[a] ~ dunif(1, 50)                 # beta2 for user proportion (Puser)
  pH_int[a] ~ dnorm(0, 0.001)             # proportion of catch harvested intercept
  pH_slo[a] ~ dnorm(0, 0.001)             # proportion of catch harvested slope
  # logit parameters for proportion estimates defined by mean, temp slope and private angler affect. 
  beta0_pelagic[a] ~ dnorm(mu_beta0_pelagic[regions[a]], tau_beta0_pelagic[regions[a]])
  beta1_pelagic[a] ~ dnorm(0, 0.001)
  beta2_pelagic[a] ~ dnorm(0, 0.001)
  beta0_yellow[a] ~ dnorm(mu_beta0_yellow[regions[a]], tau_beta0_yellow[regions[a]])
  beta1_yellow[a] ~ dnorm(0, 0.001)
  beta2_yellow[a] ~ dnorm(0, 0.001)
  beta0_black[a] ~ dnorm(mu_beta0_black[regions[a]], tau_beta0_pelagic[regions[a]])
  beta1_black[a] ~ dnorm(0, 0.001)
  beta2_black[a] ~ dnorm(0, 0.001)
	
  for(y in 16:Y){ #guided SWHS harvest likelihood
   tauHhat_ayg[a, y] <- 1 / log(cvHhat_ayg[a, y] * cvHhat_ayg[a, y] + 1)
   Hhat_ayg[a, y] ~ dlnorm(logHhat_ay[a, y] + log(pG[a, y]), tauHhat_ayg[a, y])
  }
  for(y in 3:Y){ #logbook likelihood; guided lbs harvested by area and year
   Hlb_ayg[a, y] ~ dpois(H_ayg[a, y])   # all rockfish
   Hlbp_ayg[a, y] ~ dpois(H_ayg[a, y] * p_pelagic[a, y, 1]) #pelagic rockfish
  }
  
  for(y in 11:Y){ #logbook likelihood; guided yelloweye lbs harvested by area and year
   Hlby_ayg[a, y] ~ dpois(Hy_ayg[a, y])
  }
	
  for(y in 1:Y){
   #Harvest
   H_ayg[a, y] <- H_ay[a, y] * pG[a, y]           # guided harvests
   H_ayu[a, y] <- H_ay[a, y] * (1 - pG[a, y])     # unguide harvests
   H_ay[a, y] <- exp(logH_ay[a, y])               # total harvest parameter
  
   #Black rockfish
   Hb_ayg[a, y] <- H_ayg[a, y] * p_pelagic[a, y, 1] * p_black[a, y, 1]
   Hb_ayu[a, y] <- H_ayu[a, y] * p_pelagic[a, y, 2] * p_black[a, y, 2]
   Hb_ay[a, y] <- Hb_ayg[a, y] + Hb_ayu[a, y]

   #Yelloweye rockfish
   Hy_ayg[a, y] <- H_ayg[a, y] * (1 - p_pelagic[a, y, 1]) * p_yellow[a, y, 1]
   Hy_ayu[a, y] <- H_ayu[a, y] * (1 - p_pelagic[a, y, 2]) * p_yellow[a, y, 2]
   Hy_ay[a, y] <- Hy_ayg[a, y] + Hy_ayu[a, y]
   
   #SWHS Bias and user comp
   logbc[a, y] ~ dnorm(mu_bc[a], tau_bc[a])     # mean log bias in SWHS H estimates
   pG[a, y] ~ dbeta(b1_pG[a], b2_pG[a])         # P(user)ay1 = frac of harv taken by charter anglers
   
   #SWHS total likelihood	
   tauHhat_ay[a, y] <- 1 / log(cvHhat_ay[a, y] * cvHhat_ay[a, y] + 1)
   Hhat_ay[a, y] ~ dlnorm(logHhat_ay[a, y], tauHhat_ay[a, y])
   logHhat_ay[a, y] <- logH_ay[a, y] + logbc[a, y]
   logH_ay[a, y] ~ dnorm(Htrend_ay[a, y], prec[a])
   Htrend_ay[a, y] <- Z[y, ] %*% beta[a, 1:C]   # Harvest temproal trend defined by penalized spline 

   #Proportion harvested   
   logit(pH[a, y]) <- pH_int[a] + pH_slo[a] * y
   tauChat_ay[a, y] <- 1 / log(cvChat_ay[a, y] * cvChat_ay[a, y] + 1)
   Chat_ay[a, y] ~ dlnorm(log(Hhat_ay[a, y]) - log(pH[a, y]), tauChat_ay[a, y])
   
   #Composition logistic regression
   for(u in 1:2){
    logit(p_pelagic[a, y, u]) <- beta0_pelagic[a] + beta1_pelagic[a] * y + beta2_pelagic[a] * (u - 1) + re_pelagic[a, y, u]
    logit(p_yellow[a, y, u]) <- beta0_yellow[a] + beta1_yellow[a] * y + beta2_yellow[a] * (u - 1) + re_yellow[a, y, u]
    logit(p_black[a, y, u]) <- beta0_black[a] + beta1_black[a] * y + beta2_black[a] * (u - 1) + re_black[a, y, u]
    re_black[a, y, u] ~ dnorm(0, tau_comp)    # random effects
    re_pelagic[a, y, u] ~ dnorm(0, tau_comp)
    re_yellow[a, y, u] ~ dnorm(0, tau_comp)
   }
  }
 
 #Spline parameters
 prec[a] <- pow(sigma[a], -2)
 sigma[a] <- 0.25 #~ dunif(0, 10) 
 beta[a, 1:C] ~ dmnorm(zero[1:C] + beta0[a], lambda[a] * Q[1:C, 1:C])
 gamma[a, 1:C] <- beta[a, 1:C] - beta0[a]
 lambda[a] ~ dgamma(0.001, 0.001)
 beta0[a] ~ dnorm(0, 1e-6)
 }
}