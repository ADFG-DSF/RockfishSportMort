
model{
 tau_comp ~ dgamma(0.001, 0.001)
 sd_comp <- 1 / tau_comp / tau_comp
 
 for(n in 1:N){
    comp_pelagic[n] ~ dbin(p_pelagic[comp_area[n], comp_year[n] , comp_user[n]], comp_N[n])
    comp_black[n] ~ dbin(p_black[comp_area[n], comp_year[n] , comp_user[n]], comp_pelagic[n])
    }
 
 for(r in 1:3){
  mu_beta0_pelagic[r] ~ dnorm(0, 0.001)
  mu_beta1_pelagic[r] ~ dnorm(0, 0.001)
  mu_beta2_pelagic[r] ~ dnorm(0, 0.001)
  mu_beta0_black[r] ~ dnorm(0, 0.001)
  mu_beta1_black[r] ~ dnorm(0, 0.001)
  mu_beta2_black[r] ~ dnorm(0, 0.001)
  }
  
 for (a in 1:A){
  mu_bc[a] ~ dnorm(0, 0.001)
  tau_bc[a] <- 1 / sd_bc[a] / sd_bc[a]
  sd_bc[a] ~ dunif(0, 1)
  b1[a] ~ dunif(1, 100)
  b2[a] ~ dunif(1, 100)
  pH_int[a] ~ dnorm(0, 0.001)
  pH_slo[a] ~ dnorm(0, 0.001)
  beta0_pelagic[a] ~ dnorm(mu_beta0_pelagic[regions[a]], 100)
  beta1_pelagic[a] ~ dnorm(mu_beta1_pelagic[regions[a]], 0.001)
  beta2_pelagic[a] ~ dnorm(mu_beta2_pelagic[regions[a]], 0.001)
  beta0_black[a] ~ dnorm(mu_beta0_black[regions[a]], 100)
  beta1_black[a] ~ dnorm(mu_beta1_black[regions[a]], 0.001)
  beta2_black[a] ~ dnorm(mu_beta2_black[regions[a]], 0.001)
	
  for(y in 16:Y){
   tauHhat_ayg[a, y] <- 1 / log(cvHhat_ayg[a, y] * cvHhat_ayg[a, y] + 1)
   Hhat_ayg[a, y] ~ dlnorm(log(H_ayg[a, y]) - logbc[a, y], tauHhat_ayg[a, y])
   tauHhat_ayp[a, y] <- 1 / log(cvHhat_ayp[a, y] * cvHhat_ayp[a, y] + 1)
   Hhat_ayp[a, y] ~ dlnorm(log(Hhat_ay[a, y]) + log(pU[a, y]), tauHhat_ayg[a, y])
  }
  for(y in 3:Y){
   H_ayu[a, y] <- exp(muHhat_ay[a, y]) - H_ayg[a, y]
   H_ay[a, y] <- H_ayg[a, y] + H_ayu[a, y]
  }
 
  for(y in 1:2){
   H_ayg[a, y] <- exp(muHhat_ay[a, y]) * (1 - pU[a, y])
   H_ayu[a, y] <- exp(muHhat_ay[a, y]) * pU[a, y]
   H_ay[a, y] <- H_ayg[a, y] + H_ayu[a, y]
  }
	
  for(y in 1:Y){
   logbc[a, y] ~ dnorm(mu_bc[a], tau_bc[a])
   pU[a, y] ~ dbeta(b1[a], b2[a])
		
   tauHhat_ay[a, y] <- 1 / log(cvHhat_ay[a, y] * cvHhat_ay[a, y] + 1)
   Hhat_ay[a, y] ~ dlnorm(muHhat_ay[a, y] - logbc[a, y], tauHhat_ay[a, y])
   muHhat_ay[a, y] ~ dnorm(Htrend_ay[a, y], prec[a])
   Htrend_ay[a, y] <- Z[y, ] %*% beta[a, 1:C]
		
   logit(pH[a, y]) <- pH_int[a] + pH_slo[a] * y
   tauChat_ay[a, y] <- 1 / log(cvChat_ay[a, y] * cvChat_ay[a, y] + 1)
   Chat_ay[a, y] ~ dlnorm(log(Hhat_ay[a, y]) - log(pH[a, y]), tauChat_ay[a, y])
		
   for(u in 1:2){
    logit(p_pelagic[a, y, u]) <- beta0_pelagic[a] + beta1_pelagic[a] * y + beta2_pelagic[a] * (u-1) + re_pelagic[a, y, u]
    re_pelagic[a, y, u] ~ dnorm(0, tau_comp)
	
	logit(p_black[a, y, u]) <- beta0_black[a] + beta1_black[a] * y + beta2_black[a] * (u-1) + re_black[a, y, u]
    re_black[a, y, u] ~ dnorm(0, tau_comp)
   }
  }

 prec[a] <- pow(sigma[a], -2) 
 beta[a, 1:C] ~ dmnorm(zero[1:C] + beta0[a], lambda[a] * Q[1:C, 1:C])
 gamma[a, 1:C] <- beta[a, 1:C] - beta0[a]
 lambda[a] ~ dgamma(0.001, 0.001)
 beta0[a] ~ dnorm(0, 1e-6)
 sigma[a] ~ dunif(0.5, 10) #dgamma(10, 1E8)
 }
}